#use std::mem

pub type List struct<T> {
    data: ^mut T,
    length: uint,
    capacity: uint,
};

BaseCapacity: uint = 4;

pub func (List) new<T>() -> ^mut List<T> {
    return List::newWithCapacity<T>(0);
}

pub func (List) newWithCapacity<T>(mut cap: uint) -> ^mut List<T> {
    if cap <= 0 {
        cap = BaseCapacity;
    }

    size := sizeof(T) * cap;

    res := mem::alloc<List<T>>();
    @res = List<T>{
        data: (^mut T)(mem::rawAlloc(size)),
        length: 0,
        capacity: cap,
    };

    return res;
}

pub func (it: ^List<T>) destroy() {
    mem::rawFree(uintptr(it.data));
    mem::free(it);
}

pub func (it: ^List<T>) toRawPointer() -> ^T {
    return it.data;
}

pub func (it: List<T>) get(index: uint) -> T {
    if index >= it.length {
        panic("List.get: index >= length");
    }

    return it.data[index];
}

pub func (it: List<T>) getLength() -> uint {
    return it.length;
}

pub func (it: List<T>) getCapacity() -> uint {
    return it.capacity;
}

func (it: ^mut List<T>) grow() {
    oldData := it.data;
    it.capacity *= 2;
    newData := (^mut T)(mem::rawAlloc(sizeof(T) * it.capacity));

    mut i := uint(0);
    for i < it.length {
        newData[i] = oldData[i];
        i += 1;
    }

    mem::rawFree(uintptr(oldData));
    it.data = newData;
}

/// This will peek at the top of the List
/// it will _not_ pop the value off the list.
/// Returns None if the list is empty.
pub func (it: ^mut List<T>) top() -> Option<T> {
    if it.length == 0 {
        return Option::None;
    }
    return Option::Some(it.get(it.length - 1));
}

/// Pop the last value T from the list. Note that
/// it does not clear out the popped item.
/// Returns None if the list is empty.
pub func (it: ^mut List<T>) pop() -> Option<T> {
    if it.length == 0 {
        return Option::None;
    }
    poppedValue := it.top();
    // clear old value?
    it.length -= 1;
    return poppedValue;
}

pub func (it: ^mut List<T>) append(value: T) {
    if it.length == it.capacity {
        it.grow();
    }

    it.data[it.length] = value;
    it.length += 1;
}
