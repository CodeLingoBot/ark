#use std::adt
#use std::mem

/*
	Implementation of a 
	dynamic string type.
*/

pub type String struct {
	data: ^mut adt::List<rune>,	
};

// create a dynamic string from the given
// static string
pub func (String) from(source: string) -> ^mut String {
	str := String::withCapacity(len(source));
	mut idx := uint(0);
	for idx < len(source) {
		str.append(rune(source[idx]));
		idx += 1;
	}
	return str;
}

pub func (String) withCapacity(size: uint) -> ^mut String {
	str := mem::alloc<String>();
	@str = String {
		data: adt::List::newWithCapacity<rune>(size),
	};

	mut idx := uint(0);
	for idx < size {
		str.append('\0');
		idx += 1;
	}

	return str;
}

pub func (a: ^String) pop() -> Option<rune> {
	if a.length() == 0 {
		return Option::None;
	}
	return a.data.pop();
}

pub func (a: ^String) concat(other: string) {
	mut idx := uint(0);
	for idx < len(other) {
		otherRunePointer := uintptr(other[idx]);
		a.append((rune)(otherRunePointer));
		idx += 1;
	}
}

pub func (a: ^String) append(char: rune) {
	a.data.append(char);
}

pub func (a: ^String) at(index: uint) -> rune {
	return a.data.get(index);
}

pub func (a: ^String) length() -> uint {
	return a.data.getLength();
}

pub func (a: ^String) destroy() {
	mem::free(a);
}
