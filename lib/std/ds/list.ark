#use std::mem

pub type List struct<T> {
    data: ^T,
    length: uint,
    capacity: uint,
};

BaseCapacity: uint = 4;

pub func (List) new<T>() -> ^mut List<T> {
    return List::newWithCapacity<T>(0);
}

pub func (List) newWithCapacity<T>(mut cap: uint) -> ^mut List<T> {
    if cap <= 0 {
        cap = BaseCapacity;
    }

    size := sizeof(T) * cap;

    res := mem::alloc<List<T>>();
    @res = List<T>{
        data: (^T)(mem::rawAlloc(size)),
        length: 0,
        capacity: cap,
    };

    return res;
}

pub func (it: ^List<T>) destroy() {
    mem::rawFree(uintptr(it.data));
    mem::free(it);
}

pub func (it: ^List<T>) toRawPointer() -> ^T {
    return it.data;
}

pub func (it: List<T>) get(index: uint) -> T {
    if index >= it.length {
        panic("List.get: index >= length");
    }

    return @(^T)(uintptr(it.data) + uintptr(index) * uintptr(sizeof(T)));
}

pub func (it: List<T>) getLength() -> uint {
    return it.length;
}

pub func (it: List<T>) getCapacity() -> uint {
    return it.capacity;
}

[c] func memcpy(dst: uintptr, src: uintptr, length: uint);

func (it: ^mut List<T>) grow() {
    oldData := it.data;
    it.capacity *= 2;
    it.data = (^T)(mem::rawAlloc(sizeof(T) * it.capacity));
    C::memcpy(uintptr(it.data), uintptr(oldData), it.length);
    mem::rawFree(uintptr(oldData));
}


pub func (it: ^mut List<T>) append(value: T) {
    if it.length == it.capacity {
        it.grow();
    }

    @(^T)(uintptr(it.data) + uintptr(it.length) * uintptr(sizeof(T))) = value;
    it.length += 1;
}
